#Options
GenerateScanner = true
GenerateParser = true
Language = C++
ScannerName = Lexical
ParserName = Syntactic
SemanticName = Semantic
ScannerCaseSensitive = true
ScannerTable = Full
Input = String
Parser = SLR
#RegularDefinitions

#Tokens
// Ignore
:[\n\r\t\s]*

// Comments
SINGLE_LINE_COMMENT: (/)(/)[^\n]*
MULTI_LINE_COMMENT: /\*([^\*]|[\r\n]|(\*+([^\*/]|[\r\n])))*\*+/

// Identifiers
ID : [a-zA-Z_][a-zA-Z0-9_]*

// Data Types
INT = ID : "int"
FLOAT = ID : "float"
DOUBLE = ID : "double"
CHAR = ID : "char"
STRING = ID : "string"
BOOLEAN = ID : "bool"
VOID = ID : "void"

// Reserved Keywords
IF = ID : "if"
ELSE = ID : "else"
WHILE = ID : "while"
DO = ID : "do"
FOR = ID : "for"
SWITCH = ID : "switch"
CASE = ID : "case"
RETURN = ID : "return"
CONTINUE = ID : "continue"
BREAK = ID : "break"
DEFAULT = ID : "default"
INPUT = ID : "input"
OUTPUT = ID : "print"

// Assignment
ASSIGNMENT: "="
ASSIGNMENT_ADD: "+="
ASSIGNMENT_SUB: "-="
ASSIGNMENT_MULT: "*="
ASSIGNMENT_DIVIDE: "/="
ASSIGNMENT_REMAIN: "%="

// Arithmetic Operators
INCREMENT : "++"
DECREMENT : "--"
PLUS : "+"
MINUS : "-"
MULTIPLY : "*"
DIVIDE : "/"
REMAINDER : "%"

// Logical Operators
AND : "&&"
OR : "||"
NOT : "!"

// Bitwise Operators
BIT_AND : "&"
BIT_OR : "|"
BIT_XOR : "^"
BIT_NOT : "~"
BIT_LSHIFT : "<<"
BIT_RSHIFT : ">>"

// Relational Operators
GT : ">"
LT : "<"
GE : ">="
LE : "<="
EQ : "=="
NE : "!="

// Literals
VAL_BINARY : "0b"[01]+
VAL_HEXADECIMAL : "0x"[0-9a-fA-F]+
VAL_INTEGER : [0-9]+
VAL_FLOAT : [0-9]*(\.[0-9]+)?f
VAL_DOUBLE : [0-9]*\.[0-9]+
VAL_CHAR : '[^']'
VAL_STRING: \"[^\"]*\"
VAL_TRUE = ID : "true"
VAL_FALSE = ID : "false"

// Delimiters and Punctuation
"("
")"
"["
"]"
"{"
"}"
","
";"
":"
"."
#NonTerminals
<program>
<statement_list>
<statement>
<comments>
<declaration>
<dec_assign_list>
<dec_assign>
<assignment>
<assign_array>
<expression>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<exp11>
<exp12>
<val>
<assignable_func_call>
<op_assign>
<op_rel_low>
<op_rel_high>
<op_bitwise>
<op_arit_low>
<op_arit_high>
<op_neg>
<op_inc>
<val_number>
<val_string>
<val_boolean>
<data_type>
<array_declaration>
<assignable>
<array_access>
<array_value>
<params>
<func_call>
<while_loop>
<assign_or_dec>
<for_loop>
<do_while_loop>
<loop>
<if_statement>
<switch_case>
<case_list>
<case_item>
<default_case>
<return_statement>
<break_statement>
<continue_statement>
<input>
<output>
<subroutine_def>
<subroutine_def_type>
<param_list>
<param_subroutine_def>
<block>
<no_semicolon>
#Grammar
<program> ::= <statement_list>;

// Statements
<statement_list> ::= <statement_list> <statement> ";" | <statement_list> <no_semicolon> | <statement> ";" | <no_semicolon>;
<statement> ::= <declaration>
              | <assignment>
              | <expression>
              | <return_statement>
              | <break_statement>
              | <continue_statement>
              | <input>
              | <output>;

// Comments
<comments> ::= SINGLE_LINE_COMMENT | MULTI_LINE_COMMENT;

// Declarations
<declaration> ::= <data_type> <dec_assign_list>;
<dec_assign_list> ::= <dec_assign_list> "," <dec_assign> | <dec_assign>;
<dec_assign> ::= ID ASSIGNMENT <expression> | ID <array_access> ASSIGNMENT <array_value> | ID <array_access> | ID;

// Assignments
<assignment> ::= <assignable> <op_assign> <expression> | <assign_array>;
<assign_array> ::= <assignable> <op_assign> <array_value>;

// Expression
<expression> ::= <expression> OR <exp2> | <exp2>;
<exp2> ::= <exp2> AND <exp3> | <exp3>;
<exp3> ::= <exp3> BIT_OR <exp4> | <exp4>;
<exp4> ::= <exp4> BIT_XOR <exp5> | <exp5>;
<exp5> ::= <exp5> BIT_AND <exp6> | <exp6>;
<exp6> ::= <exp6> <op_rel_low> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_rel_high> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_bitwise> <exp9> | <exp9>;
<exp9> ::= <exp9> <op_arit_low> <exp10> | <exp10>;
<exp10> ::= <exp10> <op_arit_high> <exp11> | <exp11>;
<exp11> ::= <op_neg> <exp11> | <op_inc> <assignable_func_call> | <exp12>;
<exp12> ::= <assignable> <op_inc> | <val> | "(" <expression> ")";

<val> ::= <val_number> | <val_string> | <val_boolean> | <assignable_func_call>;
<assignable_func_call> ::= <assignable> <func_call> | <assignable>;

// Operators
<op_assign> ::= ASSIGNMENT | ASSIGNMENT_ADD | ASSIGNMENT_SUB | ASSIGNMENT_MULT | ASSIGNMENT_DIVIDE | ASSIGNMENT_REMAIN;
<op_rel_low> ::= EQ | NE;
<op_rel_high> ::= GT | LT | GE | LE;
<op_bitwise> ::= BIT_LSHIFT | BIT_RSHIFT;
<op_arit_low> ::= PLUS | MINUS;
<op_arit_high> ::= MULTIPLY | DIVIDE | REMAINDER;
<op_neg> ::= MINUS | BIT_NOT | NOT;
<op_inc> ::= INCREMENT | DECREMENT;

// Values and Function Calls
<val_number> ::= VAL_BINARY | VAL_HEXADECIMAL | VAL_INTEGER | VAL_FLOAT | VAL_DOUBLE;
<val_string> ::= VAL_CHAR | VAL_STRING;
<val_boolean> ::= VAL_TRUE | VAL_FALSE;

<data_type> ::= INT | FLOAT | DOUBLE | CHAR | STRING | BOOLEAN;
<array_declaration> ::= <array_declaration> "[" "]" | "[" "]";

<assignable> ::= ID <array_access> "." <assignable> | ID <array_access> | ID "." <assignable> | ID;
<array_access> ::= "[" <expression> "]" <array_access> | "[" <expression> "]";

<array_value> ::= "[" <params> "]";
<params> ::= <params> "," <expression> | <params> "," <array_value> | <expression> | <array_value>;

// Function Call
<func_call> ::= "(" <params> ")" | "(" ")";

// Control Structures
<while_loop> ::= WHILE "(" <expression> ")" <block>;
<assign_or_dec> ::= <assignment> | <declaration>;
<for_loop> ::= FOR "(" <assign_or_dec> ";" <expression> ";" <expression> ")" <block>;
<do_while_loop> ::= DO <block> WHILE "(" <expression> ")" ";";

<loop> ::= <while_loop> | <for_loop> | <do_while_loop>;

<if_statement> ::= IF "(" <expression> ")" <block>
                 | IF "(" <expression> ")" <block> ELSE <block>
                 | IF "(" <expression> ")" <block> ELSE <if_statement>;


<switch_case> ::= SWITCH "(" <expression> ")" "{" <case_list> "}" | SWITCH "(" <expression> ")" "{" <case_list> <default_case> "}";
<case_list> ::= <case_item> <case_list> | <case_item>;
<case_item> ::= CASE <expression> ":" <statement_list>;
<default_case> ::= DEFAULT ":" <statement_list>;

<return_statement> ::= RETURN <expression>;
<break_statement> ::= BREAK;
<continue_statement> ::= CONTINUE;

// I/O and Flow Control
<input> ::= INPUT "(" <assignable> ")";
<output> ::= OUTPUT "(" <expression> ")";

// Subroutines
<subroutine_def> ::= <subroutine_def_type> "(" <param_list> ")" <block> | <subroutine_def_type> "(" ")" <block>;
<subroutine_def_type> ::= <data_type> ID | <data_type> <array_declaration> ID | VOID ID;
<param_list> ::= <param_list> "," <param_subroutine_def> | <param_subroutine_def>;
<param_subroutine_def> ::= <data_type> ID | <data_type> ID <array_declaration> | VOID ID;

// Block & No Semicolon
<block> ::= "{" <statement_list> "}";
<no_semicolon> ::= <loop>
            | <if_statement>
            | <switch_case>
            | <block>
            | <subroutine_def>
            | <comments>;