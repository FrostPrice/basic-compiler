#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Full
Input = Stream
Parser = SLR
#RegularDefinitions

#Tokens
// Ignore
:[\n\r\t\s]*

// Comments (Ignore)
:(/)(/)[^\n]*
:/(\*)([^\*]|\*+[^"\/"])*(\*)/


// Identifiers
ID : [a-zA-Z_][a-zA-Z0-9_]*

// Data Types
INT = ID : "int"
FLOAT = ID : "float"
DOUBLE = ID : "double"
CHAR = ID : "char"
STRING = ID : "string"
BOOLEAN = ID : "bool"

// Reserved Keywords
IF = ID : "if"
ELSE = ID : "else"
WHILE = ID : "while"
DO = ID : "do"
FOR = ID : "for"
SWITCH = ID : "switch"
CASE = ID : "case"
RETURN = ID : "return"
CONTINUE = ID : "continue"
BREAK = ID : "break"
DEFAULT = ID : "default"
INPUT = ID : "input"
OUTPUT = ID : "print"
VOID = ID : "void"

// Assignment
ASSIGNMENT: "="
ASSIGNMENT_ADD: "+="
ASSIGNMENT_SUB: "-="
ASSIGNMENT_MULT: "*="
ASSIGNMENT_DIVIDE: "/="
ASSIGNMENT_REMAIN: "%="

// Arithmetic Operators
INCREMENT : "++"
DECREMENT : "--"
PLUS : "+"
MINUS : "-"
MULTIPLY : "*"
DIVIDE : "/"
REMAINDER : "%"

// Logical Operators
AND : "&&"
OR : "||"
NOT : "!"

// Bitwise Operators
BIT_AND : "&"
BIT_OR : "|"
BIT_XOR : "^"
BIT_NOT : "~"
BIT_LSHIFT : "<<"
BIT_RSHIFT : ">>"

// Relational Operators
GT : ">"
LT : "<"
GE : ">="
LE : "<="
EQ : "=="
NE : "!="

// Literals
VAL_BINARY : "0b"[01]+
VAL_HEXADECIMAL : "0x"[0-9a-fA-F]+
VAL_INTEGER : [0-9]+
VAL_FLOAT : [0-9]*(\.[0-9]+)?f
VAL_DOUBLE : [0-9]*\.[0-9]+
VAL_CHAR : '[^']'
VAL_STRING: \"[^\"]*\"
VAL_TRUE = ID : "true"
VAL_FALSE = ID : "false"

// Delimiters and Punctuation
"("
")"
"["
"]"
"{"
"}"
","
";"
":"
"."
"<<"
">>"
#NonTerminals
<program>
<instruction_type>
<instruction>
<instruction_set>
<declaration>
<dec_assign_list>
<dec_assign>
<dec_target>
<assignment>
<expression>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<exp11>
<exp12>
<op_assign>
<op_rel_low>
<op_rel_high>
<op_bitwise>
<op_arit_low>
<op_arit_high>
<op_neg>
<op_inc>
<val_number>
<val_string>
<val_boolean>
<data_type>
<array_declaration>
<assignable>
<access_chain>
<access_element>
<prop_access>
<array_access>
<array_access_list>
<array_value>
<params>
<func_call>
<block>
<statement_list>
<while_loop>
<for_loop>
<do_while_loop>
<loop>
<conditional>
<if_statement>
<if_else_statement>
<switch_case>
<case_list>
<case_item>
<default_case>
<return_statement>
<break_statement>
<continue_statement>
<input>
<input_list>
<input_list_rest>
<output>
<output_list>
<output_list_rest>
<output_item>
<variable>
<subroutine_call>
<arg_list>
<arg_list_rest>
<subroutine_def>
<param_list>
<param_list_rest>
<param>
<statement>
<val>
<assignable_func_call>

#Grammar
<program> ::= <instruction_set>;

<instruction_type> ::= <declaration> | <assignment>;
<instruction> ::= <instruction_type> ";";
<instruction_set> ::= <instruction_set> <instruction> | <instruction>;

<declaration> ::= <data_type> <dec_assign_list>;

<dec_assign_list> ::= <dec_assign_list> "," <dec_assign> | <dec_assign>;
<dec_assign> ::= <dec_target> ASSIGNMENT <expression>
  | <dec_target> ASSIGNMENT <array_value>
  | <dec_target>;
<dec_target> ::= ID <array_access_list> | ID;

<assignment> ::= <assignable> <op_assign> <expression>;

<expression> ::= <exp1>;
<exp1> ::= <exp1> OR <exp2> | <exp2>;
<exp2> ::= <exp2> AND <exp3> | <exp3>;
<exp3> ::= <exp3> BIT_OR <exp4> | <exp4>;
<exp4> ::= <exp4> BIT_XOR <exp5> | <exp5>;
<exp5> ::= <exp5> BIT_AND <exp6> | <exp6>;
<exp6> ::= <exp6> <op_rel_low> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_rel_high> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_bitwise> <exp9> | <exp9>;
<exp9> ::= <exp9> <op_arit_low> <exp10> | <exp10>;
<exp10> ::= <exp10> <op_arit_high> <exp11> | <exp11>;
<exp11> ::= <op_neg> <exp11> | <op_inc> <assignable> | <exp12>;
<exp12> ::= <assignable> <op_inc> | <val> | "(" <expression> ")";

<val> ::= <val_number> | <val_string> | <val_boolean> | <assignable_func_call>;
<assignable_func_call> ::= <assignable> <func_call> | <assignable>;

<op_assign> ::= ASSIGNMENT | ASSIGNMENT_ADD | ASSIGNMENT_SUB | ASSIGNMENT_MULT | ASSIGNMENT_DIVIDE | ASSIGNMENT_REMAIN;
<op_rel_low> ::= EQ | NE;
<op_rel_high> ::= GT | LT | GE | LE;
<op_bitwise> ::= BIT_LSHIFT | BIT_RSHIFT;
<op_arit_low> ::= PLUS | MINUS;
<op_arit_high> ::= MULTIPLY | DIVIDE | REMAINDER;
<op_neg> ::= MINUS | BIT_NOT | NOT;
<op_inc> ::= INCREMENT | DECREMENT;

<val_number> ::= VAL_BINARY | VAL_HEXADECIMAL | VAL_INTEGER | VAL_FLOAT | VAL_DOUBLE;
<val_string> ::= VAL_CHAR | VAL_STRING;
<val_boolean> ::= VAL_TRUE | VAL_FALSE;

<data_type> ::= INT | FLOAT | DOUBLE | CHAR | STRING | BOOLEAN;
<array_declaration> ::= "[" "]" <array_declaration> | "[" "]";

<assignable> ::= ID <array_access> "." <assignable> | ID <array_access> | ID "." <assignable> | ID;
<array_access> ::= "[" <expression> "]" <array_access> | "[" <expression> "]";

<array_value> ::= "[" <params> "]";
<params> ::= <expression> "," <params> | <array_value> "," <params> | <expression> | <array_value>;

<func_call> ::= "(" <params> ")" <func_call> | "(" <params> ")" | "(" ")" <func_call> | "(" ")";

<block> ::= "{" <statement_list> "}";
<statement_list> ::= <statement> <statement_list> | î;

<while_loop> ::= WHILE "(" <expression> ")" <block>;
<for_loop> ::= FOR "(" <assignment> ";" <expression> ";" <assignment> ")" <block>;
<do_while_loop> ::= DO <block> WHILE "(" <expression> ")" ";";

<loop> ::= <while_loop> | <for_loop> | <do_while_loop>;

<conditional> ::= <if_statement> | <if_else_statement>;
<if_statement> ::= IF "(" <expression> ")" <block>;
<if_else_statement> ::= IF "(" <expression> ")" <block> ELSE <block>;

<switch_case> ::= SWITCH "(" <expression> ")" "{" <case_list> <default_case> "}";
<case_list> ::= <case_item> <case_list> | î;
<case_item> ::= CASE <val_number> ":" <statement_list>;
<default_case> ::= DEFAULT ":" <statement_list> | î;

<return_statement> ::= RETURN <expression> ";";
<break_statement> ::= BREAK ";";
<continue_statement> ::= CONTINUE ";";

<input> ::= INPUT ">>" <input_list> ";";
<input_list> ::= <variable> <input_list_rest>;
<input_list_rest> ::= ">>" <variable> <input_list_rest> | î;

<output> ::= OUTPUT "<<" <output_list> ";";
<output_list> ::= <output_item> <output_list_rest>;
<output_list_rest> ::= "<<" <output_item> <output_list_rest> | î;
<output_item> ::= <variable> | VAL_STRING | VAL_INTEGER | VAL_FLOAT | VAL_DOUBLE | VAL_CHAR | VAL_TRUE | VAL_FALSE;

<variable> ::= ID | ID "[" <expression> "]";
<subroutine_call> ::= ID "(" <arg_list> ")";
<arg_list> ::= <expression> <arg_list_rest> | î;
<arg_list_rest> ::= "," <expression> <arg_list_rest> | î;

<subroutine_def> ::= <data_type> ID "(" <param_list> ")" <block>;
<param_list> ::= <param> <param_list_rest> | î;
<param_list_rest> ::= "," <param> <param_list_rest> | î;
<param> ::= <data_type> <variable>;

<statement> ::= <declaration>
            | <assignment>
            | <loop>
            | <conditional>
            | <switch_case>
            | <return_statement>
            | <break_statement>
            | <continue_statement>
            | <input>
            | <output>
            | <block>
            | <subroutine_def>
            | <subroutine_call> ";";
